# Maintainer: valadaa48

epoch=1

_pkgname=RetroArch
pkgname=retroarch-odroidgo2
pkgver=55438.5e551dd92b
pkgrel=1
pkgdesc="Retroarch for odroidgo2"
arch=('aarch64')
url="https://github.com/libretro/RetroArch"
license=('GPL')
makedepends=('git' 'libdrm-rockchip')
depends=(
  alsa-lib
  freetype2
  glibc
  libass
  libpng
  libxkbcommon
  librga
  openal
  zlib
)
conflicts=('retroarch')
provides=('retroarch')
source=(
  'git+https://github.com/libretro/RetroArch#commit=5e551dd92b79d8127e66939835ea3c2a140c4078'
  'retroarch.cfg'
  'retroarch.service'
)
backup=('etc/retroarch.cfg')
md5sums=('SKIP'
         'ba2da9cd6b6fc81c75b6d9a1a4bf65f5'
         'a68f468e4eb5afa92b37d4e916dba57e')

pkgver() {
  cd "$_pkgname"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"
  #    sed -i 's:drm/drm_fourcc.h:libdrm/drm_fourcc.h:' deps/libgo2/src/display.c gfx/drivers/oga_gfx.c
}

build() {
  export CXXFLAGS="$CXXFLAGS -fno-rtti -std=gnu++11"
  export CFLAGS="$CFLAGS"

  cd "$_pkgname"
  ./configure \
    --prefix=/usr \
    --disable-builtinminiupnpc \
    --disable-caca \
    --disable-discord \
    --disable-jack \
    --disable-miniupnpc \
    --disable-opengl \
    --disable-opengl1 \
    --enable-ozone \
    --disable-pulse \
    --disable-qt \
    --disable-sdl \
    --disable-sdl2 \
    --disable-tinyalsa \
    --disable-libusb \
    --disable-v4l2 \
    --disable-wayland \
    --disable-x11 \
    --disable-vulkan \
    --disable-ffmpeg \
    --disable-xvideo \
    --enable-alsa \
    --enable-egl \
    --enable-freetype \
    --enable-kms \
    --enable-odroidgo2 \
    --enable-opengles \
    --enable-opengles3 \
    --enable-udev

  make V=1
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../retroarch.cfg -t "$pkgdir"/etc

  install -Dm644 ../retroarch.service -t "$pkgdir"/usr/lib/systemd/system
}

